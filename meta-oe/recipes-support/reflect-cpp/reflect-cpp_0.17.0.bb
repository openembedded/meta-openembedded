DESCRIPTION = "A C++20 library for fast serialization, deserialization and validation using reflection."
HOMEPAGE = "https://github.com/getml/reflect-cpp"

LICENSE = "MIT"
LIC_FILES_CHKSUM = "file://LICENSE;md5=34cfa8d5bb7702c509b244b91ac4d987"

DEPENDS = "ctre"

SRC_URI = "git://github.com/getml/reflect-cpp.git;protocol=https;branch=main"
SRCREV = "b57a52ace54dae39793ab572b4cd4711cf6e3079"

S = "${WORKDIR}/git"

inherit cmake

PACKAGECONFIG ??= "json cbor flexbuffers xml ubjson yaml"
PACKAGECONFIG[json] = "-DREFLECTCPP_JSON=ON,-DREFLECTCPP_JSON=OFF,yyjson"
PACKAGECONFIG[cbor] = "-DREFLECTCPP_CBOR=ON,-DREFLECTCPP_CBOR=OFF,jsoncons"
PACKAGECONFIG[flexbuffers] = "-DREFLECTCPP_FLEXBUFFERS=ON,-DREFLECTCPP_FLEXBUFFERS=OFF,flatbuffers"
PACKAGECONFIG[xml] = "-DREFLECTCPP_XML=ON,-DREFLECTCPP_XML=OFF,pugixml"
PACKAGECONFIG[ubjson] = "-DREFLECTCPP_UBJSON=ON,-DREFLECTCPP_UBJSON=OFF,jsoncons"
PACKAGECONFIG[yaml] = "-DREFLECTCPP_YAML=ON,-DREFLECTCPP_YAML=OFF,yaml-cpp"

EXTRA_OECMAKE:append = " \
-DBUILD_SHARED_LIBS=ON \
-DREFLECTCPP_AVRO=OFF \
-DREFLECTCPP_BSON=OFF \
-DREFLECTCPP_CAPNPROTO=OFF \
-DREFLECTCPP_MSGPACK=OFF \
-DREFLECTCPP_TOML=OFF \
-DREFLECTCPP_BUILD_BENCHMARKS=OFF \
-DREFLECTCPP_BUILD_TESTS=OFF \
-DREFLECTCPP_USE_BUNDLED_DEPENDENCIES=OFF \
-DREFLECTCPP_USE_VCPKG=OFF \
"
